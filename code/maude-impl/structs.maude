fmod AST is
    pr QID .
    sorts System Rule Equation Operation Var Term TermList .
    subsorts Rule Equation < System .
    subsorts Var < Term < TermList .
    op emptySys : -> System [ctor] .
    op _;_ : System System -> System [ctor assoc comm id: emptySys prec 36] .
    op _===_ : Term Term -> Equation [ctor prec 34] .
    op _==>_ : Term Term -> Rule [ctor prec 34] .
    op _=[_]=>_ : Term Qid Term -> Rule [ctor prec 34] .
    op nil : -> TermList [ctor] .
    op _,_ : TermList TermList -> TermList [ctor assoc id: nil prec 30] .
    op var : Qid -> Var [ctor] .
    op oper : Qid -> Operation [ctor] .
    op _!_ : Operation TermList -> Term [ctor prec 32] .
    eq R::Rule ; R::Rule = R::Rule .
    eq E::Equation ; E::Equation = E::Equation .
endfm

fth GRAPH-SPEC is
    sorts NodeData EdgeData .
endfth

fmod GRAPH{S :: GRAPH-SPEC} is
    pr NAT .
    sorts Node{S} Edge{S} Graph{S} .
    subsorts Edge{S} < Graph{S} .
    op _@_ : Nat S$NodeData -> Node{S} [prec 30] .
    op _through_to_ : Node{S} S$EdgeData Node{S} -> Edge{S} [ctor prec 34] .
    op empty : -> Graph{S} [ctor] .
    op _;_ : Graph{S} Graph{S} -> Graph{S} [ctor assoc comm id: empty prec 36] .
    ops nextId nextIdAux : Graph{S} -> Nat .
    vars I0 I1 : Nat .
    vars ND0 ND1 : S$NodeData .
    vars N0 N1 : Node{S} .
    var ED : S$EdgeData .
    var E : Edge{S} .
    var G : Graph{S} .
    eq E ; E = E .
    eq nextIdAux(empty) = 0 .
    eq nextIdAux(((I0 @ ND0) through ED to (I1 @ ND1)) ; G) = max(max(I0, I1), nextId(G)) .
    eq nextId(G) = nextIdAux(G) + 1 .
endfm

fmod PEG-SPEC is
    pr NAT . pr QID .
    sorts SystemD RuleD CompositionD OperationD VarD NodeData Nothing EdgeData .
    subsorts SystemD RuleD CompositionD OperationD VarD < NodeData .
    subsorts Nothing Nat < EdgeData .
    op sysD : -> SystemD [ctor] .
    op anonRuleD : -> RuleD [ctor] .
    op namedRuleD : Qid -> RuleD [ctor] .
    op anonCompD : -> CompositionD [ctor] .
    op namedCompD : Qid -> CompositionD [ctor] .
    op operD : Qid -> OperationD [ctor] .
    op varD : Qid -> OperationD [ctor] .
    op nothing : -> Nothing [ctor] .
endfm

view PEGSpec from GRAPH-SPEC to PEG-SPEC is
endv

fmod PEG is
    pr PEG-SPEC + GRAPH{PEGSpec} * (
        sort Node{PEGSpec} to PNode,
        sort Edge{PEGSpec} to PEdge,
        sort Graph{PEGSpec} to Peg .
    ) .
    op _into_ : PNode PNode -> PEdge [prec 34] .
    vars N0 N1 : PNode .
    eq N0 into N1 = N0 through nothing to N1 .
endfm

fmod EPEG is
    pr PEG .
    sorts EPeg EquivClass EquivClasses .
    subsorts Nat < EquivClass < EquivClasses .
    op __ : EquivClass EquivClass -> EquivClasses [ctor assoc comm prec 38] .
    op _,_ : EquivClasses EquivClasses -> EquivClasses [ctor assoc comm prec 40] .
    op _*!_ : Peg EquivClasses -> EPeg [ctor prec 42] .
    var I : Nat .
    var EC : EquivClass .
    var ECS : EquivClasses .
    eq I I = I .
    eq EC , EC = EC .
endfm
